// Copyright (c) 2003-2006 King's College London, created by Laurence Tratt
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to
// deal in the Software without restriction, including without limitation the
// rights to use, copy, modify, merge, publish, distribute, sublicense, and/or
// sell copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in
// all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
// FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS
// IN THE SOFTWARE.


import Builtins, Exceptions, Strings, Thread
import CPK::Earley::Grammar
import Compiler::Core, Compiler::Parser, Compiler::Eval, Compiler::ITree, Compiler::ITree_PP, Compiler::ITree_Rename, Compiler::ITree_WF, Compiler::Lift, Compiler::Tokenizer




func compiler():

    return Core::peek_compiler()



func error(msg, src_infos := null):

    Core::peek_compiler().error(msg, src_infos)



func warning(msg, src_infos := null):

    Core::peek_compiler().warning(msg, src_infos)



func eval(str, vars := Dict{}, includes := []):

    return Eval::eval(str, vars, includes)



func fresh_name(name := null):

    return Core::fresh_name(name)



func pp_itree(itree):

    return ITree_PP::pp(itree)



func elided_pp_itree(itree, self_mod_id := null):

    return ITree_PP::elided_pp(itree, self_mod_id)



func rename_itree(itree, rename_vars := null):

    if rename_vars is null:
        return ITree_Rename::rename_fresh(itree)
    else:
        return ITree_Rename::rename_(itree)



func embeddable_itree(itree, except_vars := Set{}):

    if Builtins::List.instantiated(itree):
        itrees := itree
    else:
        itrees := [itree]

    all_vars := Set{}
    for itree := itrees.iter():
        all_vars.extend(itree.free_vars)
        all_vars.extend(itree.bound_vars)
    
    rename_vars := all_vars.complement(except_vars)
    
    renames := Dict{}
    rename_elems := []
    for var := rename_vars.iter():
        fn := fresh_name(var)
        renames[var] := fn
        rename_elems.append(irename(ivar(var), ivar(fn)))

    return [ITree_Rename::rename_(itree, renames), rename_elems]



func lift(obj, src_infos := null):

    return Lift::lift(obj, src_infos)



func dsl_parse(dsl_block, src_infos, extra_keywords, extra_symbols, grammar, start_rule):

    tokenizer := Tokenizer::Tokenizer.new()
    tokenizer.tokenize(dsl_block, src_infos, extra_keywords, extra_symbols)
    
    tokens_map := Tokenizer::tokens_map(extra_symbols, extra_keywords)
    grammar, rule_names := Grammar::compile(grammar, start_rule, tokens_map)
    parser := Parser::Parser.new()
    
    return parser.parse(grammar, rule_names, tokens_map, tokenizer.tokens)



//
// This function creates all the factory functions for ITree elements.
//

func _ibuilder():

    builders := []
    for name, defn := ITree.iter_defns():
        if name[0] != "I" | not Builtins::Class.instantiated(defn):
            continue
        num_params := ITree::IInt.new(defn.get_field("init").num_params, __SRC_INFO__)
        builders.append([|
            func $c{ITree::IVar.new(name.lower_cased(), __SRC_INFO__)}(*args):
            
                // Add src info from the calling function.

                if args.len() == ${num_params} - 1:
                    args := args[ : -1] + [(args[-1] + Thread::get_continuation_src_infos(2))]
                else:
                    args := args + [Thread::get_continuation_src_infos(2)]

                // Create the element and immediately type check it.

                elem := ITree::$c{ITree::IVar.new(name, __SRC_INFO__)}.new.apply(args)
                ITree_WF::wf(elem)
                
                return elem
        |])
    
    return builders


$c<_ibuilder()>
